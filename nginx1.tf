# This file defines a sample NGINX application deployment.
# It includes the Kubernetes namespace, deployment, service, ingress,
# certificate, and the corresponding Cloudflare DNS record.
# This serves as a template for deploying web applications on the cluster.

# Creates the "nginx1" namespace to isolate the NGINX application resources.
resource "kubernetes_namespace" "nginx1" {

  depends_on = [
    time_sleep.wait_for_kubernetes
  ]

  metadata {
    name = "nginx1"
  }
}


# Deploys a single replica of NGINX.
# The deployment manages the lifecycle of the NGINX pods.
resource "kubernetes_deployment" "nginx1" {

  depends_on = [
    kubernetes_namespace.nginx1
  ]

  metadata {
    name      = "nginx1"
    namespace = "nginx1"
    labels = {
      app = "nginx1"
    }
  }

  spec {
    replicas = 1

    selector {
      match_labels = {
        app = "nginx1"
      }
    }

    template {
      metadata {
        labels = {
          app = "nginx1"
        }
      }

      spec {
        container {
          image = "nginx:latest"
          name  = "nginx"

          port {
            container_port = 80
          }
        }
      }
    }
  }
}


# Creates a ClusterIP service to expose the NGINX deployment within the cluster.
# This service provides a stable internal IP address for the NGINX pods.
resource "kubernetes_service" "nginx1" {

  depends_on = [
    kubernetes_namespace.nginx1
  ]

  metadata {
    name      = "nginx1"
    namespace = "nginx1"
  }
  spec {
    selector = {
      app = "nginx1"
    }
    port {
      port = 80
    }

    type = "ClusterIP"
  }
}


# Creates a Certificate resource for the "nginx1" application.
# cert-manager will use this resource to request a TLS certificate from
# the "cloudflare-prod" ClusterIssuer for the specified domain.
resource "kubectl_manifest" "nginx1-certificate" {

  depends_on = [kubernetes_namespace.nginx1, time_sleep.wait_for_clusterissuer]

  yaml_body = <<YAML
apiVersion: cert-manager.io/v1
kind: Certificate
metadata:
  name: nginx1
  namespace: nginx1
spec:
  secretName: nginx1
  issuerRef:
    name: cloudflare-prod
    kind: ClusterIssuer
  dnsNames:
  - 'domain'
    YAML
}


# Creates an Ingress resource to expose the NGINX service to the internet.
# It routes traffic for the specified host to the "nginx1" service and
# configures TLS using the secret generated by cert-manager.
resource "kubernetes_ingress_v1" "nginx1" {

  depends_on = [kubernetes_namespace.nginx1]

  metadata {
    name      = "nginx1"
    namespace = "nginx1"
  }

  spec {
    rule {

      host = "domain"

      http {

        path {
          path = "/"

          backend {
            service {
              name = "nginx1"
              port {
                number = 80
              }
            }
          }

        }
      }
    }

    tls {
      secret_name = "nginx1"
      hosts       = ["domain"]
    }
  }
}

# Creates a Cloudflare DNS "A" record to point the domain to the
# public IP address of the Traefik load balancer.
resource "cloudflare_record" "clcreative-main-cluster" {
  zone_id = "zone-id"
  name    = "domain"
  value   = data.civo_loadbalancer.traefik_lb.public_ip
  type    = "A"
  proxied = false
}